#
# A clean but detailed theme that displays relevant, contextual information.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Fotis Gimian <fgimiansoftware@gmail.com>
#

# Load dependencies.
pmodload 'helper'

function python_version {
  if [[ ! -z $PYENV_VERSION ]]; then
    _python_version=$PYENV_VERSION
  elif [[ ! -z $VIRTUAL_ENV ]]; then
    _python_version=$(basename $VIRTUAL_ENV)
  elif [[ -f .python-version ]]; then
    _python_version=$(cat .python-version)
  elif [[ -f ~/.pyenv/version ]]; then
    _python_version=$(cat ~/.pyenv/version)
  else
    _python_version=system
  fi
}

function prompt_sorin_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_sorin_pwd="$MATCH"
    unset MATCH
  else
    _prompt_sorin_pwd="${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}/${pwd:t}"
  fi
}

function prompt_sorin_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_sorin_pwd

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Get Python version information.
  python_version
}

function prompt_sorin_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Stop virtualenv from changing our prompt
  VIRTUAL_ENV_DISABLE_PROMPT=1

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info and getting Python version before each command.
  add-zsh-hook precmd prompt_sorin_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %B%F{red}>%F{yellow}>%F{green}>%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{red}⌂%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{green}<%F{yellow}<%F{red}<%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ':%%B%F{yellow}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format ' %%B%F{green}✚%f%%b'
  zstyle ':prezto:module:git:info:ahead' format ' %%B%F{yellow}↑%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%B%F{yellow}↓%f%%b'
  zstyle ':prezto:module:git:info:branch' format ':%F{green}%b%f'
  zstyle ':prezto:module:git:info:commit' format ':%F{green}%.7c%f'
  zstyle ':prezto:module:git:info:deleted' format ' %%B%F{red}✖%f%%b'
  zstyle ':prezto:module:git:info:modified' format ' %%B%F{blue}✱%f%%b'
  zstyle ':prezto:module:git:info:position' format ':%F{red}%p%f'
  zstyle ':prezto:module:git:info:renamed' format ' %%B%F{magenta}➜%f%%b'
  zstyle ':prezto:module:git:info:stashed' format ' %%B%F{cyan}✭%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %%B%F{yellow}⸗%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %%B%F{white}○%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' ' %F{blue}git%f$(coalesce "%b" "%p" "%c")%s' \
    'rprompt' '%A%B%S%a%d%m%r%U%u'

  # Define main prompt
  PROMPT='%F{cyan}%n@%m%f'                               # Host info (username@hostname)
  PROMPT+=' '
  PROMPT+='%F{blue}${_prompt_sorin_pwd}%f'               # Current Directory
  PROMPT+='%(!. %B%F{red}#%f%b. %F{green}>%f) '          # Prompt (> or #)

  # Define the right prompt
  RPROMPT='${git_info[rprompt]}'                         # Git status symbol
  RPROMPT+='${git_info:+${(e)git_info[prompt]}}'         # Git branch info (git:<branch>)
  RPROMPT+=' '
  RPROMPT+='%F{blue}py%f:%F{green}${_python_version}%f'  # Git version info (py:<version>)

  # Define the suggestion prompt
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_sorin_setup "$@"
